---
page_title: "Provider: GoDaddy DNS"
description: |-
  Manage DNS resource records for domains hosted on GoDaddy
---

# GoDaddy DNS management plugin: `godaddy-dns`

This plug-in allows for managment of individual DNS resource records for domains hosted on GoDaddy servers.

It only manages DNS (no e.g. domain management) and aims to manage individual DNS resource records (not the whole domain), while preserving existant records and tolerating external modifications.

## Usage

Provider configuration is simple and usually empty, providing that authentication info is set in environment variables `GODADDY_API_KEY` and `GODADDY_API_SECRET` (see [GoDaddy API docs](https://developer.godaddy.com/) for instructions on how to get them). Alternatively, they can be set in `api_key` and `api_secret` parameters in the provider configuration.<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `api_key` (String, Sensitive) GoDaddy API key
- `api_secret` (String, Sensitive) GoDaddy API secret

## DNS Record resource : `dns_record`

DNS entries are described as instances of `dns_records` resource.

It currently supports `A`, `AAAA`, `CNAME`, `MX`, `NS` and `TXT` records. `SRV` are not supported; if anyone hosting AD on GoDaddy or uses them for VOIP or something like that, please let me know by creating an issue.

Example usage:

```terraform
provider "godaddy-dns" {
  api_key    = "better set it in GODADDY_API_KEY"
  api_secret = "better set it in GODADDY_API_SECRET"
}

# create "alias.test.com" as CNAME for "other.com"
resource "godaddy-dns_record" "my-cname" {
  domain = "test.com"
  type   = "CNAME"
  name   = "alias"
  data   = "other.com"
}
```

To set several records, use `for_each`:

```terraform
terraform {
  required_providers {
    godaddy-dns = {
      source = "registry.terraform.io/veksh/godaddy-dns"
    }
  }
}

# keys from env
provider "godaddy-dns" {}

# struct for several records
locals {
  domain = "mydomain.com"
  records = {
    "mx" = {
      type = "MX",
      name = "_test-cli",
      data = "mx1.pseudo.com",
      prio = 10,
    },
    "txt" = {
      type = "TXT",
      name = "_test-cli",
      data = "also, txt",
    },
  }
}

# with names like `godaddy-dns_record.array-of-records["mx"]`
resource "godaddy-dns_record" "array-of-records" {
  for_each = local.records
  domain   = local.domain
  type     = each.value.type
  name     = each.value.name
  data     = each.value.data
  priority = lookup(each.value, "prio", null)
}
```

See `dns_record` docs for additional examples.

## Differences vs alternative providers

Differences vs n3integration provider and its forks:
- granularity: top-level configuration object is record, not domain, no need to put the whole domain under terraform management
- updates to one record do not result in scary plans threatening to destroy all the domain contents
- external modifications are mostly ok: update will not complain if record is already set to the desired state, delete will be happy if record is already gone
- `destroy` is fully supported
